group 'com.haskforce'

version = file('version.txt').text.trim()

sourceCompatibility = 1.8

ext {
  javaVersion = '1.8'
}

repositories {
  mavenCentral()
}

intellij {
  version = '2017.3'
}

// Export our version so we can validate it at runtime.
task genHaskForceCoreMeta {
  doLast {
    mkdir("build/generated-src/java/com/haskforce/core")
    def f = file("build/generated-src/java/com/haskforce/core/HaskForceCoreMeta.java")
    f.write("""
      package com.haskforce.core;

      public interface HaskForceCoreMeta {
        String VERSION = \"$version\";
      }
    """.stripIndent().trim())
  }
}

sourceSets.main.java.srcDirs 'src/main/java', 'build/generated-src/java'

compileJava.dependsOn(genHaskForceCoreMeta)
