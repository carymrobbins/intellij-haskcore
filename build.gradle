plugins {
  id 'org.jetbrains.intellij' version '0.3.1'
}

version = file('version.txt').text.trim()

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'

sourceCompatibility = 1.8

ext {
  javaVersion = '1.8'
}

repositories {
  mavenCentral()
}

intellij {
  version = '2017.3'
  pluginName = 'HaskForce-Core'
  updateSinceUntilBuild = false
}

static def getGitRev() {
  def proc = "git rev-parse --short HEAD".execute()
  proc.waitFor()
  return proc.text.trim()
}

static def isGitDirty() {
  return "git diff-index --quiet HEAD".execute().waitFor() != 0
}

def getPluginVersion() {
  if (version.endsWith('SNAPSHOT')) {
    if (isGitDirty()) {
      return "$version-${getGitRev()}-dirty"
    } else {
      return "$version-${getGitRev()}"
    }
  } else {
    return version
  }
}

patchPluginXml {
  version = getPluginVersion()
}

buildPlugin {
  archiveName = "haskforce-core-${patchPluginXml.version}.zip"
}
