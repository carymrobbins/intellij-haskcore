plugins {
  id 'org.jetbrains.intellij' version '0.3.1'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.intellij'

group 'com.haskforce'

version = updateVersion(file('version.txt').text.trim())

sourceCompatibility = 1.8

ext {
  javaVersion = '1.8'
}

repositories {
  mavenCentral()
}

intellij {
  version = '2017.3'
}

static def updateVersion(String version) {
  if (version.endsWith('SNAPSHOT')) {
    def proc = "git rev-parse --short HEAD".execute()
    proc.waitFor()
    def gitRev = proc.text.trim()
    def isGitDirty = "git diff-index --quiet HEAD".execute().waitFor() != 0
    if (isGitDirty) {
      return "$version-$gitRev-dirty"
    } else {
      return "$version-$gitRev"
    }
  } else {
    return version
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    mavenLocal()
  }
}
